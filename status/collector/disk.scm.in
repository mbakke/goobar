;;; This file is part of Goobar.
;;;
;;; Copyright Â© 2023 Marius Bakke
;;;
;;; Goobar is free software: you can redistribute it and/or modify it
;;; under the terms of the GNU General Public License as published by
;;; the Free Software Foundation, either version 3 of the License, or
;;; (at your option) any later version.
;;;
;;; Goobar is distributed in the hope that it will be useful, but
;;; WITHOUT ANY WARRANTY; without even the implied warranty of
;;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
;;; GNU General Public License for more details.
;;;
;;; You should have received a copy of the GNU General Public License
;;; along with Goobar. If not, see <https://www.gnu.org/licenses/>.

(define-module (status collector disk)
  #:use-module (status)
  #:use-module (ice-9 popen)
  #:use-module (ice-9 textual-ports)
  #:use-module (ice-9 match)
  #:use-module (ice-9 format)
  #:export (disk-status))

;; TODO: Remove this and read-disk below when Guile with statvfs is released.
(define (read-df mount-point)
  (let* ((input (open-input-pipe (string-append "@DF@"
                                                " -B1 --output=size,used,avail "
                                                mount-point)))
         (data (get-string-all input)))
    (close-port input)
    ;; Drop the first line of data (header).
    (let ((lines (string-split data #\newline)))
      ;; The output is '(SIZE USED AVAIL).
      (map string->number (string-tokenize (string-join (cdr lines) "\n"))))))

(define (read-statvfs mount-point)
  (match (statvfs mount-point)
    (#(bsize fsize total free avail rest ...)
     (let ((used (- total avail)))
       (map (lambda (blocks)
              (* blocks fsize))
            (list total used avail))))))

(define read-disk read-@STATVFS_OR_DF@) ;chosen at build time

(define (percentage->number threshold)
  (string->number (string-drop-right threshold 1)))

(define* (disk-status mount-point
                      #:key
                      (format format-disk-status)
                      (bad-threshold "95%")
                      (degraded-threshold "90%"))
  (if (file-exists? mount-point)
      (let ((bad (percentage->number bad-threshold))
            (degraded (percentage->number degraded-threshold)))
        (match (read-disk mount-point)
          ((total used avail)
           ;; TODO: Implement K/M/G/T conversion and thresholds.
           (let ((percent-used (ceiling (* 100 (/ used total)))))
             (make-status
              'disk
              (cond ((> percent-used bad) 'bad)
                    ((> percent-used degraded) 'degraded)
                    (else 'neutral))
              `((mount-point . ,mount-point)
                ;; Force GiB for now.
                (used . ,(ceiling (/ used 1024 1024 1024)))
                (available . ,(ceiling (/ avail 1024 1024 1024)))
                (percent-used . ,percent-used))
              format)))))
      (make-status 'disk 'bad mount-point format-mount-point-not-found)))

(define (format-mount-point-not-found status)
  (format #f "~a: not found" (status-data status)))

(define (format-disk-status status)
  (let ((data (status-data status)))
    (format #f "~d%" (assoc-ref data 'percent-used))))
